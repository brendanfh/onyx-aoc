#include_file "core/std/wasi"

use package core

part_1 :: proc (contents: ^str) -> u32 {
    chars : [26] bool;
    for ^ch: chars do *ch = false;

    while true {
        line: str;
        string.read_line(contents, ^line);
        if line.count == 0 do break;

        for ch: line do chars[~~ch - cast(u32) #char "a"] = true;
    }

    sum := 0;
    for ch: chars do if ch do sum += 1;

    return sum;
}

part_2 :: proc (contents: ^str) -> u32 {
    chars : [26] u32;
    for ^ch: chars do *ch = 0;

    person_count := 0;
    while true {
        line: str;
        string.read_line(contents, ^line);
        if line.count == 0 do break;
        person_count += 1;

        for ch: line do chars[~~ch - cast(u32) #char "a"] += 1;
    }

    sum := 0;
    for ch: chars do if ch == person_count do sum += 1;

    return sum;
}

main :: proc (args: [] cstr) {
    contents := file.get_contents("input/day6.txt");
    contents_data := contents.data;
    defer cfree(contents_data);

    unique_sum := 0;
    while true {
        if contents.count == 0 do break;

        unique := part_1(^contents);
        unique_sum += unique;
    }

    printf("Unique sum: %i\n", unique_sum);
}
